// Parameters
real theta = 4.*pi/3.;
real a = 2.; //The length of the semimajor axis
real b = 1.; //The length of the semiminor axis
func z =0;

// Mesh
border Gamma1(t=0., theta){x=a*cos(t); y=b*sin(t);}
border Gamma2(t=theta, 2.*pi){x=a*cos(t); y=b*sin(t);}
mesh Th = buildmesh(Gamma1(10) + Gamma2(5));

plot(Th);

// Fespace
fespace Vh(Th, P2); //P2 conforming triangular FEM
Vh phi, w; 

// Solve
solve Laplace(phi, w)
    = int2d(Th)(
          dx(phi)*dx(w)
        + dy(phi)*dy(w)
    )
    - int2d(Th)(
          exp(-10*(x^2+y^2))*w
    )
    + on(Gamma1, phi=z)
    ;

// Plot
plot(phi,  fill=true,ps="membrane.eps"); //Plot phi
//plot(Th,  ps="membraneTh.eps"); //Plot Th

// Save mesh
savemesh(Th,"Th.msh");

